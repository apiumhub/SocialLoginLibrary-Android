apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.apiumhub.social"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        manifestPlaceholders = [fbAppId: "179933089418674", fbLoginProtocolScheme: "fb179933089418674"]
    }

    signingConfigs {
        debug {
            storeFile file("../ext/keystore.jks")
            storePassword '123456'
            keyAlias 'debugKey'
            keyPassword '123456'
        }
        release {
            storeFile file("../ext/keystore.jks")
            storePassword '123456'
            keyAlias 'releaseKey'
            keyPassword '123456'
        }
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    // Ensure the no-op dependency is always used in JVM tests.
    configurations.all { config ->
        if (config.name.contains('UnitTest')) {
            config.resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                    details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.stdlib

    implementation deps.support.design

    debugImplementation deps.leakCanaryDebug
    releaseImplementation deps.leakCanaryRelease

    kapt deps.databindingCompiler

    kaptTest deps.databindingCompiler

    testImplementation deps.junit
    testImplementation deps.kotlin.test
    compile project(path: ':library')
}
repositories {
    mavenCentral()
}
